rule star_index:
    """
    Genome indexation for RNA-STAR mapper

    Required input:
        __star_index__fasta: the reference genome to indexed in FASTA format

    Required output:
        __star_index__output_done: done file for STAR mapping rule

    params:

        __star_index__output_dir: the directory where write the index

    """


    input:
       fasta =  __star_index__fasta
    output:
        __star_index__output_done
    params:
        wkdir = __star_index__output_dir
    log:
        __star_index__log
    threads:
        config['star_mapping']['threads']
    run:
        from sequana import FastA
        ff = FastA(input.fasta)
        genome_length = sum(ff.lengths)
        import math
        Nbases = min(14, math.floor(math.log2(genome_length)/2 - 1))
        shell(f"STAR --runMode genomeGenerate --genomeFastaFiles {input.fasta} --genomeDir {params.wkdir} --runThreadN {threads} --genomeSAindexNbases {Nbases}")
        shell("samtools faidx {input.fasta}")
